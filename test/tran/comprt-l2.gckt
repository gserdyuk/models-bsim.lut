** One-Bit Comparator (Tran): Benchmarking Implementation of BSIM3 by Jane Xi 5/16/2005.

.options reltol=0.1
.options abstol=1.e-6
.include modelcard_lut.nmos
.include modelcard_lut.pmos


M1 Anot A Vdd Vdd  P1 W=3.6u L=1.2u     lut_mode=2
M2 Anot A 0 0 N1 W=1.8u L=1.2u          lut_mode=2
M3 Bnot B Vdd Vdd  P1 W=3.6u L=1.2u     lut_mode=2
M4 Bnot B 0 0 N1 W=1.8u L=1.2u          lut_mode=2
M5 AorBnot 0 Vdd Vdd P1 W=1.8u L=3.6u   lut_mode=2
M6 AorBnot B 1 0 N1 W=1.8u L=1.2u       lut_mode=2
M7 1 Anot 0 0 N1 W=1.8u L=1.2u          lut_mode=2
M8 Lnot 0 Vdd Vdd P1 W=1.8u L=3.6u      lut_mode=2
M9 Lnot Bnot 2 0 N1 W=1.8u L=1.2u       lut_mode=2
M10 2 A 0 0 N1 W=1.8u L=1.2u            lut_mode=2
M11 Qnot 0 Vdd Vdd P1 W=3.6u L=3.6u     lut_mode=2
M12 Qnot AorBnot 3 0 N1 W=1.8u L=1.2u   lut_mode=2
M13 3 Lnot 0 0 N1 W=1.8u L=1.2u         lut_mode=2
MQLO 8 Qnot Vdd Vdd  P1 W=3.6u L=1.2u   lut_mode=2
MQL1 8 Qnot 0 0 N1 W=1.8u L=1.2u        lut_mode=2
MLTO 9 Lnot Vdd Vdd P1 W=3.6u L=1.2u    lut_mode=2
MLT1 9 Lnot 0 0 N1 W=1.8u L=1.2u        lut_mode=2
CQ Qnot 0 30f
CL Lnot 0 10f

Vdd Vdd 0 1.8
Va A 0  pulse 0 1.8 10ns .1ns .1ns 15ns 30ns
Vb B 0 0


.print tran  v(a) v(b) v(9) v(8) 
.tran 1ns 60ns basic
*.option post

.end
